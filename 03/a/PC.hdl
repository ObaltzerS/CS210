// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Put your code here:

    Inc16(in=m1, out=m1inc); // Increment m1, the current output
    Mux16(a=m1, b=m1inc, sel=inc, out=m2); // select between current output and incremented output
    Mux16(a=m2, b=in, sel=load, out=m3); // select between new load in or current output
    Mux16(a=m3, b=false, sel=reset, out=m4); // select between reset or current output
    Register(in=m4, load=true, out=out, out=m1); // load value into register and provide current output
    
}
